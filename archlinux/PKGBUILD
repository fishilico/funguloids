# Contributor: Dany Martineau <dany.luc.martineau at gmail.com>
# Maintainer: Nicolas Iooss <nicolas.iooss_aur at m4x.org

pkgname=funguloids
pkgver=1.06.4
_realver=1.06-4
pkgrel=6
pkgdesc="3D game about collecting mushrooms in outerspace"
arch=('i686' 'x86_64')
url="http://funguloids.sourceforge.net/"
license=('GPL')
depends=('ogre' 'ois' 'freealut' 'lua51' 'libogg' 'libvorbis' 'libmad')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-linux-${_realver}.tar.bz2
        funguloids.desktop
        funguloids-alc_error.patch
        funguloids-gcc44.patch
        funguloids-ogre_1.6.patch funguloids-strcmp.patch
        size_chunks_reverse.patch
        mpak.py
        funguloids-lua.patch
        openalsoundsystem.cpp.diff
        funguloids-ogre-1.7.0.patch
        funguloids-compilation.patch
        funguloids-boost-system.patch
        funguloids-ogre-1.9.0.patch)
md5sums=('1d9d92b08f58715d3dcb59c2ebcb7db7'
         '3c970b99c50b0ba1af72d1d0d41db752'
         '534ad262a561eab8e28a5a28827020fe'
         '8a183ffa27d468435106dc4417c58db1'
         '34c898ca360f92ad140e5322d409088a'
         '742ccc09463858d2c884cc348fed1f76'
         '16ec314d20bfd7deda866cb5be80ae2d'
         '31601d57ad749396898ab074ed602cf9'
         'c1f2bb3ced905304586ea7055f04af44'
         '01ce094dba98bd99a255792935fe07c0'
         'dd05749fc642ccca87bc8115b65b3fa4'
         'cb0be4fe216a1a7cfd83d5fa427752f5'
         'a483f7e882381fca74b2058ea5be0679'
         '357c4138733fe1003625fc9b0f0c11b2')

prepare() {
  cd "${srcdir}/${pkgname}"

  # Convert DOS line ending to Unix by removing every CR character
  find . \( -name '*.h' -o -name '*.cpp' -o -name '*.in' -o -name '*.am' \) \
    -exec sed 's/\r//' -i {} \;

  patch -p0 < ../openalsoundsystem.cpp.diff
  patch -p1 < ../funguloids-gcc44.patch
  patch -p0 < ../funguloids-ogre_1.6.patch
  patch -p0 < ../funguloids-strcmp.patch
  patch -p0 < ../size_chunks_reverse.patch
  patch -p0 < ../funguloids-lua.patch
  patch -p1 < ../funguloids-ogre-1.7.0.patch
  patch -p1 < ../funguloids-compilation.patch
  patch -p1 < ../funguloids-boost-system.patch
  patch -p1 < ../funguloids-ogre-1.9.0.patch

  sed -i -e 's;cp bootstrap.mpk "@gameinstalldir@";cp bootstrap.mpk "$(DESTDIR)@gameinstalldir@";' -e 's;funguloids.mpk "@gameinstalldir@";funguloids.mpk "$(DESTDIR)@gameinstalldir@";' bin/Makefile.in
  sed -i -e 's;cp MarylandInMay.ogg "@musicinstalldir@";cp MarylandInMay.ogg "$(DESTDIR)@musicinstalldir@";' bin/music/Makefile.in
  sed -i -e 's;-llua5.1;-llua;' -e 's;share/games/funguloids;share/funguloids;' \
      -e 's;bininstalldir="${prefix}/games;bininstalldir="${prefix}/bin;' configure.ac
  chmod +x ../mpak.py
  ../mpak.py -e -f bin/bootstrap.mpk -p _bootstrap
  ../mpak.py -e -f bin/funguloids.mpk -p _gamedata
  sed -ri '/^[A-Z]/ s/(.*)/overlay \1/' _bootstrap/*.overlay _gamedata/*.overlay
  sed -ri '/^[A-Z]/ s/(.*)/particle_system \1/' _gamedata/*.particle
  sed -ri 's/^(\t\t\t)(texture_unit) 1/\1\2\n\1{\n\1}\n\1\2/' _gamedata/materials.material
  ../mpak.py -c -f bin/bootstrap.mpk _bootstrap/*
  ../mpak.py -c -f bin/funguloids.mpk _gamedata/*
  rm -rf _bootstrap _gamedata

  rm -f build-aux/*
  aclocal
  autoheader
  autoconf
  automake --add-missing
  ./configure --prefix=/usr
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR=${pkgdir} install

  # install desktop file
  install -Dm644 ${srcdir}/funguloids.desktop \
    ${pkgdir}/usr/share/applications/funguloids.desktop
}
